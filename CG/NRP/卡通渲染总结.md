# 卡通渲染总结

https://zhuanlan.zhihu.com/p/619582749

https://zhuanlan.zhihu.com/p/648899016



**如何让角色看起来卡通**

思考一下，究竟是哪些因素，让我们觉得角色是卡通的呢。我觉得可以先从下面3点入手

1.减少色阶数量

2.冷暖色调分离

3.对明暗区域的手绘控制



**减少色阶数量**

![img](https://pic1.zhimg.com/80/v2-6b42dee8a385cb8d5350b2d7fd562fe2_720w.webp)

减少色阶的数量，给画面卡通感



**冷暖色调分离**

![img](https://pic1.zhimg.com/80/v2-50e5f648742d9400b19659c8f6c32e32_720w.webp)

明面和暗面分配不同冷暖的颜色，给画面卡通感

在美术上根据颜色区分为暖色调（红色，黄色）和冷色调（蓝色、紫色）。在偏真实的光照计算中，往往只计算一个明暗关系，然后由光和物体的颜色决定最终效果。而卡通渲染则会根据明暗关系，为明面和暗面分配不同色调的颜色。比如一个暖色调的明面，配合一个冷色调的暗面。将色调拉开以后，更进一步给人卡通感。[相关链接](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=1&q=相关链接&zhida_source=entity) [tone-based-shading](https://link.zhihu.com/?target=http%3A//users.cs.northwestern.edu/~ago820/thesis/node26.html)

![img](https://picx.zhimg.com/80/v2-9f120dee6861babbc17790551ed14d65_720w.webp)

《GUILTY GEAR Xrd》中通过单独的贴图定义暗面色调，和明面的色调做区分。左图是未调整暗面色调，右图是调整了暗面色调的

在《GUILTY GEAR Xrd》游戏中，绘制了一张称为SSS Texture的贴图，来对暗面的色调进行调整。



**对明暗区域的手绘控制**

在[手绘动画](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=1&q=手绘动画&zhida_source=entity)中为了好的画面效果，往往其明暗的分布并不是完全正确的。最明显的，角色的脖子部分通常都出现明显的阴影。经典光照计算的结果是非常“正确”的，因而缺少卡通的手绘感。需要用其他方式对光照的计算结果进行调整。

![img](https://pic3.zhimg.com/80/v2-51fd93dfd71712e9b553bbcd683e3e76_720w.webp)

按照正确的光照计算，角色的脖子是不会有那么明显的阴影的

下面介绍一下《GUILTY GEAR Xrd》中是如何对明暗区域进行手绘控制的

![img](https://picx.zhimg.com/80/v2-b4b878262270a3858e5b2a3564a6b74d_720w.webp)

在《GUILTY GEAR Xrd》中，通过灯光方向，Threshold贴图，法线方向对光照计算进行手绘风格的控制

**灯光方向控制：**

卡通渲染的角色在部分灯光方向下，可以有最佳的画面表现。有时候这个灯光方向和场景灯光或者其他角色的灯光方向不一致。为了让每个角色都有最佳表现，最好每个角色有一盏自己的灯光方向。甚至当这个角色转向时，这个灯光也跟着角色做一定程度的转向，来让角色有一个更好的光影表现。

**Threshold贴图控制：**

《GUILTY GEAR Xrd》中将这张贴图称作ilmTexture。为了减少歧义，我们这里也这么称呼好了。

![img](https://pic3.zhimg.com/80/v2-237068d312d4eb63d96aa5520fb45974_720w.webp)

ilmTexture关闭和开启对角色阴影区域的影响

这张贴图有些类似于AO贴图，不过它是对光照计算的结果进行一些倾向性的修正。让一部分区域，比如角色脖子的部分更容易产生阴影。来达到手绘风格的阴影效果。

**法线方向控制：**

法线控制有两种方法，一种是直接编辑法线，达到想要的光照结果。一种是创建一个平滑的简单模型，然后将其法线传递到复杂物体上，达到优化阴影的效果。Maya自带法线传递的功能，3ds Max可以通过插件[Noors Normal Thief](https://link.zhihu.com/?target=http%3A//www.scriptspot.com/3ds-max/scripts/noors-normal-thief)实现法线传递的功能。

![img](https://pic2.zhimg.com/80/v2-c679ae70fee5f6fefb5b0ff23452466d_720w.webp)

通过直接编辑法线，达到想要的光照效果

![img](https://picx.zhimg.com/80/v2-f91894ea2232f5e5ce96ee56e33a55d5_720w.webp)

创建简单的头套模型，传递其法线到头发上，优化头发的阴影

![img](https://pic3.zhimg.com/80/v2-dc8604c3b0473b0b468a72ece018ff18_720w.webp)

在《火影忍者 究极风暴》中，将人物的面部向外膨胀，再用膨胀后的面部法线来优化面部阴影



**赛璐璐风格插画**

![img](https://pic4.zhimg.com/80/v2-4d9211cbaa5bc06f9b993e15be2c1e0f_720w.webp)

《百变小樱》动画的赛璐璐片

赛璐璐片是一种塑料卡片，在早期日本动画制作流程中的，画师会在赛璐璐材质的塑料卡片上对原画进行上色。其特点为通常只有明暗2个色阶，明暗变化的交界非常明显。现在这种风格的卡通渲染比较流行。在本篇中，也将实现偏向这种风格的卡通渲染。

**厚涂风格插画**

![img](https://pic4.zhimg.com/80/v2-0eae949bf5d0ccae74986c3138551177_720w.webp)

《明日方舟》塔露拉立绘

厚涂风格相较赛璐璐风格，色阶更多，明暗交界变化会柔和很多。这个风格也有它的好处，因为3D场景比较难做成赛璐璐的。如何让赛璐璐风格的角色和非赛璐璐的场景融合是也许需要考虑的。厚涂风格的角色会更容易和场景进行融合。



**双色阶的渲染实现**

首先我们实现一个明暗边界分明的光照效果，并支持分别设置明暗区域的颜色，设置暗面颜色为冷色调，和明面的色调做出区分。

```text
Shader "Unlit/CelRender"
{
    Properties
    {
        _MainTex ("MainTex", 2D) = "white" {}
        _MainColor("Main Color", Color) = (1,1,1)
	_ShadowColor ("Shadow Color", Color) = (0.7, 0.7, 0.8)
	_ShadowRange ("Shadow Range", Range(0, 1)) = 0.5
        _ShadowSmooth("Shadow Smooth", Range(0, 1)) = 0.2

        [Space(10)]
	_OutlineWidth ("Outline Width", Range(0.01, 2)) = 0.24
        _OutLineColor ("OutLine Color", Color) = (0.5,0.5,0.5,1)
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }

        pass
        {
           Tags {"LightMode"="ForwardBase"}
			 
            Cull Back
            
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
			
	    #include "UnityCG.cginc"
	    #include "Lighting.cginc"
            #include "AutoLight.cginc"

            sampler2D _MainTex; 
	    float4 _MainTex_ST;
            half3 _MainColor;
	    half3 _ShadowColor;
            half _ShadowRange;

            struct a2v 
	   {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv : TEXCOORD0;
            };

            struct v2f
	   {
                float4 pos : SV_POSITION;
                float2 uv : TEXCOORD0;
                float3 worldNormal : TEXCOORD1;
		float3 worldPos : TEXCOORD2; 
            };


            v2f vert(a2v v)
	  {
                v2f o;
		UNITY_INITIALIZE_OUTPUT(v2f, o);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
		o.worldNormal = UnityObjectToWorldNormal(v.normal);
		o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                o.pos = UnityObjectToClipPos(v.vertex);
                return o;
            }

            half4 frag(v2f i) : SV_TARGET 
	   {
                half4 col = 1;
                half4 mainTex = tex2D(_MainTex, i.uv);
                half3 viewDir = normalize(_WorldSpaceCameraPos.xyz - i.worldPos.xyz);
		half3 worldNormal = normalize(i.worldNormal);
                half3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
		half halfLambert = dot(worldNormal, worldLightDir) * 0.5 + 0.5;
                half3 diffuse = halfLambert > _ShadowRange ? _MainColor : _ShadowColor;
                diffuse *= mainTex;
                col.rgb = _LightColor0 * diffuse;
                return col;
            }
            ENDCG
        }

        Pass
	{
                //描边，参考上一篇
        }
    }
}
```

![img](https://pic1.zhimg.com/80/v2-adfd56e2481fa8ff90311add479f0630_720w.webp)

实现明暗边界分明的光照，并且单独设置明面和暗面的颜色来区分色调



**smoothstep柔化明暗边界**

现在我们希望能够对明暗边界的变化做一些柔化，让风格往厚涂的风格靠一些，这样可以跟更容易地跟一些非赛璐璐风格的场景做融合。这里我们使用**smoothstep**函数实现这个效果。这个函数可以在根据输入数据，计算一个范围在0到1区间的平滑过渡曲线。通过这个函数的结果对明面和暗面的颜色进行[插值](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=1&q=插值&zhida_source=entity)，来实现明暗边界的软硬控制。[wiki百科链接](https://link.zhihu.com/?target=https%3A//en.wikipedia.org/wiki/Smoothstep)

![img](https://pica.zhimg.com/80/v2-0d2c3bbb17c02c8d4250374b27d7dbde_720w.webp)

smoothstep(0,0.7,x)

对代码进行如下修改

```text
          half halfLambert = dot(worldNormal, worldLightDir) * 0.5 + 0.5;
          half ramp = smoothstep(0, _ShadowSmooth, halfLambert - _ShadowRange);
          half3 diffuse = lerp(_ShadowColor, _MainColor, ramp);
```

![img](https://pic2.zhimg.com/80/v2-c1fd6a19b1f89c81f100ba16c03e5a2f_720w.webp)

使用smoothstep函数对明暗分界的软硬进行控制



**Ramp贴图**

还有一个做法是通过采样Ramp贴图来实现对色阶和明暗边界的控制。可以看成是用标准光照的结果为UV，采样一张用作颜色映射表的贴图，通过这张贴图控制光照计算的结果。制作如下图的ramp贴图，然后对代码进行修改。

```text
	half halfLambert = dot(worldNormal, worldLightDir) * 0.5 + 0.5;
        half ramp =  tex2D(_rampTex, float2(saturate(halfLambert - _ShadowRange), 0.5)).r;
        half3 diffuse = lerp(_ShadowColor,_MainColor, ramp);
```

![img](https://picx.zhimg.com/80/v2-7fdc4832dea7e32fd95867164a541a5f_720w.webp)

Ramp贴图，从左向右对应光照从0到1的范围。注意最左边光照应该最弱的部分反而设置的比较亮

![img](https://pic2.zhimg.com/80/v2-ec69ddea615a0b2d3c842fdce7a26911_720w.webp)

使用Ramp贴图对色阶进行控制，在暗面形成了一个有点亮的反光

Ramp贴图能够更容易的定义多个色阶，不过贴图需要自己制作。贴图制作起来并不复杂，也可以通过编写编辑器工具来生成。这里的贴图是使用Toony Colors Pro插件生成的。

在制作Ramp贴图的时候，最左边光照应该最弱的部分反而设置的比较亮。这个是为了制作出暗面的反光效果。在素描上面有个[明暗五调子](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=1&q=明暗五调子&zhida_source=entity)的知识，在物体边缘的部分会有一圈反光，所以物体的边缘不会是最暗的部分。[链接](https://link.zhihu.com/?target=http%3A//blog.sina.com.cn/s/blog_5fb4564f0102x8ht.html)



![img](https://picx.zhimg.com/80/v2-1ff270a4a577c1623c1136e0158188ff_720w.webp)

素描关系上的三大面五大调子

在图形学上，也有对应的概念，称为[菲涅耳](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=1&q=菲涅耳&zhida_source=entity)（fresnel）现象。我觉得这体现了一个非常有趣的观点，无论是[图形学](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=2&q=图形学&zhida_source=entity)使用光照模型对现实世界的物理现象进行模拟，还是画家们通过观察现实世界总结出的[美术理论](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=1&q=美术理论&zhida_source=entity)，最终都是殊途同归的。一个使用公式进行绘图，一个使用画笔进行绘图罢了。



在《偶像大师》系列，也使用了Ramp贴图来实现色阶和色调的控制

![img](https://picx.zhimg.com/80/v2-249d24936300177a2a084cafeeee160b_720w.webp)

左上的贴图是《偶像大师1》的ramp贴图，左下是《偶像大师2》的ramp贴图

![img](https://picx.zhimg.com/80/v2-1a872fae5e9f56268a06e436ffe2bc7f_720w.webp)

《偶像大师1》（左）和《偶像大师2》（右）的画面变化

因为本篇的篇幅有点太长了，有关边缘反光部分的讨论，放在下一篇再详细讨论。



**ilmTexture贴图的实现**

《GUILTY GEAR Xrd》中使用称为ilmTexture的贴图对角色明暗区域实现手绘风格的控制。其中绿通道控制[漫反射](https://zhida.zhihu.com/search?content_id=112580631&content_type=Article&match_order=1&q=漫反射&zhida_source=entity)的阴影阈值，红通道控制高光强度，蓝通道控制高光范围。这里跟据这个原理，完成一个最简单的实现。卡通渲染不像Lambert等光照模型有统一的公式，如果要更进一步的表现还需要根据画面需求做各种trick。比如专栏标题的展示图片还添加了阴影残留和阴影色调分离的效果，这方面就由大家自己发挥吧。

```text
Shader "Unlit/CelRenderFull"
{
	Properties
	{
		_MainTex ("MainTex", 2D) = "white" {}
                _IlmTex ("IlmTex", 2D) = "white" {}

		[Space(20)]
		_MainColor("Main Color", Color) = (1,1,1)
		_ShadowColor ("Shadow Color", Color) = (0.7, 0.7, 0.7)
		_ShadowSmooth("Shadow Smooth", Range(0, 0.03)) = 0.002
		_ShadowRange ("Shadow Range", Range(0, 1)) = 0.6

		[Space(20)]
		_SpecularColor("Specular Color", Color) = (1,1,1)
		_SpecularRange ("Specular Range",  Range(0, 1)) = 0.9
                _SpecularMulti ("Specular Multi", Range(0, 1)) = 0.4
		_SpecularGloss("Sprecular Gloss", Range(0.001, 8)) = 4

		[Space(20)]
		_OutlineWidth ("Outline Width", Range(0, 1)) = 0.24
                _OutLineColor ("OutLine Color", Color) = (0.5,0.5,0.5,1)
	}

	SubShader
	{
		Pass
		{
			Tags { "LightMode"="ForwardBase"}

			CGPROGRAM
                        #pragma vertex vert
                        #pragma fragment frag
			#pragma multi_compile_fwdbase
			
			#include "UnityCG.cginc"
			#include "Lighting.cginc"
                        #include "AutoLight.cginc"

                        sampler2D _MainTex; 
			float4 _MainTex_ST;
                        sampler2D _IlmTex; 
			float4 _IlmTex_ST;

			half3 _MainColor;
			half3 _ShadowColor;
			half _ShadowSmooth;
			half _ShadowRange;
			
			half3 _SpecularColor;
			half _SpecularRange;
        	        half _SpecularMulti;
			half _SpecularGloss;

			struct a2v
			{
				float4 vertex : POSITION;
				float2 uv : TEXCOORD0;
				float3 normal : NORMAL;
			};

			struct v2f
			{
				float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;	
				float3 worldNormal : TEXCOORD1;
				float3 worldPos : TEXCOORD2; 
			};
			
			v2f vert (a2v v)
			{
				v2f o = (v2f)0;
				o.pos = UnityObjectToClipPos(v.vertex);
				o.uv = TRANSFORM_TEX(v.uv, _MainTex);
				o.worldNormal = UnityObjectToWorldNormal(v.normal);
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				return o;
			}
			
			half4 frag (v2f i) : SV_Target
			{
				half4 col = 0;
				half4 mainTex = tex2D (_MainTex, i.uv);
				half4 ilmTex = tex2D (_IlmTex, i.uv);
				half3 viewDir = normalize(_WorldSpaceCameraPos.xyz - i.worldPos.xyz);
				half3 worldNormal = normalize(i.worldNormal);
				half3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);

				half3 diffuse = 0;
				half halfLambert = dot(worldNormal, worldLightDir) * 0.5 + 0.5;
				half threshold = (halfLambert + ilmTex.g) * 0.5;
				half ramp = saturate(_ShadowRange  - threshold); 
				ramp =  smoothstep(0, _ShadowSmooth, ramp);
				diffuse = lerp(_MainColor, _ShadowColor, ramp);
				diffuse *= mainTex.rgb;

				half3 specular = 0;
				half3 halfDir = normalize(worldLightDir + viewDir);
				half NdotH = max(0, dot(worldNormal, halfDir));
				half SpecularSize = pow(NdotH, _SpecularGloss);
				half specularMask = ilmTex.b;
				if (SpecularSize >= 1 - specularMask * _SpecularRange)
				{
					specular = _SpecularMulti * (ilmTex.r) * _SpecularColor;
				}

				col.rgb = (diffuse + specular) * _LightColor0.rgb;
				return col;
			}
			ENDCG
		}

                Pass
                {
                        //描边，参考上一篇
                }
	}
	FallBack Off
}
```

![img](https://pica.zhimg.com/80/v2-788929ac2ffb3acba1d100affb202542_720w.webp)

上色后的效果



**边缘光和后处理在卡通渲染中的作用**

边缘光是卡通渲染中重要的组成部分。它和描边一样都在物体的轮廓出现。卡通渲染通常饱和度比较高，描边和边缘光能够让角色从高饱和度的画面中凸显出来。在动画中也经常用边缘光经常用来表现逆光效果。之前《阴阳师》出过一段名为《离岛之歌》的CG。我非常喜欢。下面是其中的截图。

![img](https://pic1.zhimg.com/80/v2-193405b5ada91bf593cad8b7c64d3dc4_720w.webp)

《离岛之歌》CG截图

图片中除了边缘光效果外，还比较明显的表现了Bloom效果。Bloom后处理是卡通渲染中比较常见的后处理。 在上图中，通过让边缘光部分的颜色溢出到周围区域，能提高画面的通透感，并且让角色更好的跟周围场景融合。下面将这两个效果也加入到工程中。



**边缘光的实现**

首先实现一个基本的边缘光， 在上一篇的光照计算中，加入下面代码，得到一个基础的边缘光效果。

```text
    half f =  1.0 - saturate(dot(viewDir, worldNormal));
    half3 rimColor = f * _RimColor.rgb *  _RimColor.a;
    col.rgb = (diffuse + specular + rimColor) * _LightColor0.rgb;
```

![img](https://pic2.zhimg.com/80/v2-412d1784b095b31da0e955922eb4370b_720w.webp)

在卡通渲染中，边缘光的过度通常会比较硬，所以需要对边缘光的软硬和范围进行控制。这里我们和上一章一样采用**smoothstep**函数来帮助我们解决问题。对代码进行如下修改

```text
    half f =  1.0 - saturate(dot(viewDir, worldNormal));
    half rim = smoothstep(_RimMin, _RimMax, f);
    rim = smoothstep(0, _RimSmooth, rim);
    half3 rimColor = rim * _RimColor.rgb *  _RimColor.a;
```

![img](https://pic2.zhimg.com/80/v2-1ebcd519831d166d1c54cbf34ebb585b_720w.webp)

使用smoothstep函数调整边缘光的范围和软硬

我们将边缘光应用到角色模型上，和上一章的效果做比对。能够看到角色的轮廓更加凸显了。

![img](https://picx.zhimg.com/80/v2-020241de1609b1ec2cb6862e579d359b_720w.webp)

无边缘光（左）和有边缘光（右）的对比



**边缘光的Mask控制**

边缘光的计算使用的是法线点乘视线。在物体的[法线](https://zhida.zhihu.com/search?content_id=112937959&content_type=Article&match_order=2&q=法线&zhida_source=entity)和视线垂直的时候，边缘光会很强。在球体上不会有问题，但是在一些有平面的物体，当平面和视线接近垂直的时候，会导致整个平面都有边缘光。这会让一些不该有边缘光的地方出现边缘光。为了解决这个问题，在《GUILTY GEAR Xrd》中使用边缘光的Mask贴图来对边缘光区域进行调整。

![img](https://pica.zhimg.com/80/v2-6536ccf9fbd7f58d970e9b75027038b0_720w.webp)

《GUILTY GEAR Xrd》中的边缘光Mask调整



**Bloom后处理的实现**

Bloom本身作为常见的后处理，在unity的各种插件，或者网络上的文章上面都有实现。这里就不讲Bloom后处理的实现。不过卡通渲染对Bloom后处理有一些特别的要求。常见的Bloom后处理是根据颜色（或者说是亮度）来决定曝光程度的的。但是卡通渲染的Bloom，需要能够自己控制曝光区域，与这个区域的颜色和亮度无关。

![img](https://pic2.zhimg.com/80/v2-011de8ebb3b0662ce8c1a248dcdc28bd_720w.webp)

以上图的做举例，角色的衣服是白色的，颜色上属于亮度最高的。但是这里我们希望衣服上不要有过多曝光。曝光能集中在角色边缘光的位置。在这里我们采用将颜色写入Alpha通道，利用[Alpha值](https://zhida.zhihu.com/search?content_id=112937959&content_type=Article&match_order=1&q=Alpha值&zhida_source=entity)来控制曝光度的方法。这需要大家自行对Bloom进行一定的修改。

用Alpha通道控制Bloom的曝光，在渲染半透明物体的时候会遇到问题，因为半透明混合也需要用到Alpha。这点可以通过使用额外的Pass单独写入Alpha来解决。[相关文章](https://zhuanlan.zhihu.com/p/36076204)



![img](https://pic3.zhimg.com/80/v2-a12ffc4b5e2deb8738ef13d39d66d5c2_720w.webp)

在《离岛之歌》中截图中，bloom的曝光主要集中在光照方向，边缘光的部分。这里对边缘光的公式做点小trick。将边缘光乘以[漫反射](https://zhida.zhihu.com/search?content_id=112937959&content_type=Article&match_order=1&q=漫反射&zhida_source=entity)公式，来获得比较符合光照方向边缘光。将它的值赋给Alpha通道。

```text
    half3 worldNormal = normalize(i.worldNormal);
    half3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
    half NdotL = max(0, dot(worldNormal, worldLightDir));
    half rimBloom = pow (f, _RimBloomExp) * _RimBloomMulti * NdotL;
    col.a = rimBloom;
```

![img](https://pic1.zhimg.com/80/v2-37aa6320fdf8a92c0a10356e1e9dd59e_720w.webp)

通过Alpha通道控制的曝光范围（左）和曝光的溢色效果（右）



添加Bloom效果后，提高的角色的受光感，画面显得更加通透。实际上Bloom也有助于角色和背景的融合，以及对画面有一定的润色效果。

![img](https://pic4.zhimg.com/80/v2-a98f8dbcdf062fce39cda7c9b33c4bbd_720w.webp)

未开启Bloom（左）和开启Bloom（右）的对比



前面所有的效果叠加起来，再加上背景。终于可以完成题图的效果了。

![img](https://picx.zhimg.com/80/v2-959c178a335f1b76a93ab92764d4cf1f_720w.webp)